{
  parserClass="com.sumologic.epigraph.schema.parser.SchemaParser"

  elementTypeHolderClass="com.sumologic.epigraph.schema.parser.lexer.SchemaElementTypes"
  elementTypePrefix="S_"
  elementTypeClass="com.sumologic.epigraph.schema.parser.lexer.SchemaElementType"
  tokenTypeClass="com.sumologic.epigraph.schema.parser.lexer.SchemaElementType"

  psiClassPrefix="Schema"
  psiImplClassSuffix="Impl"
  psiPackage="com.sumologic.epigraph.schema.parser.psi"
  psiImplPackage="com.sumologic.epigraph.schema.parser.psi.impl"
  psiImplUtilClass="com.sumologic.epigraph.schema.parser.psi.impl.SchemaPsiImplUtil"
  psiTreeUtilClass="com.sumologic.epigraph.schema.parser.psi.util.SchemaPsiTreeUtil"

  generateTokenAccessors=true

  tokens=[
    NAMESPACE = 'namespace'
    IMPORT = 'import'
    MAP = 'map'
    DEFAULT = 'default'
    NODEFAULT = 'nodefault'
    LIST = 'list'
    RECORD = 'record'
    EXTENDS = 'extends'
    VARTYPE='vartype'
    ENUM='enum'
    META='meta'
    SUPPLEMENT='supplement'
    SUPPLEMENTS='supplements'
    WITH='with'
    POLYMORPHIC='polymorphic'
    ABSTRACT='abstract'
    OVERRIDE='override'

    INTEGER_T='integer'
    LONG_T='long'
    DOUBLE_T='double'
    BOOLEAN_T='boolean'
    STRING_T='string'

    BACKTICK = '`'
    COLON = ':'
    DOT = '.'
    COMMA = ','
    EQ = '='
    CURLY_LEFT = '{'
    CURLY_RIGHT = '}'
    BRACKET_LEFT = '['
    BRACKET_RIGHT = ']'
    STAR = '*'

    space='regexp:\s+'
    comment='regexp://.*'
    block_comment='regexp:/\*((?<!\*/)(.|\n))*'

    id = 'regexp:\p{Alpha}\p{Alnum}*'

    // for data lang
    NULL = 'null'
    SLASH = '/'
    PAREN_LEFT = '('
    PAREN_RIGHT = ')'
    ANGLE_LEFT = '<'
    ANGLE_RIGHT = '>'
    number="regexp:([0-9])+"
    string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"
  ]

  pin(".*Body") = 1
  //pin(".*TypeDef") = 1
  //recoverWhile(".*Body") = "bodyRecover"
  recoverWhile(".*Part") = "partRecover"

  // extends(".*.TypeDef") = "typeDef" // types hierarchy doesn't work with stubs
  implements(".*TypeDef")="com.sumologic.epigraph.schema.parser.psi.SchemaTypeDef"

  implements(".*TypeBody") = "com.sumologic.epigraph.schema.parser.psi.CustomParamsHolder"
  extends(".*TypeBody") = "com.sumologic.epigraph.schema.parser.psi.impl.CustomParamHolderImpl"
}

// consider separating fields/types/decl parts by either ',' or newlines,
// otherwise stuff can get confusing, e.g.
// record { f1:t1 f2:t2 }

root ::= namespaceDecl imports defs

// either quoted or unquoted id
private qid ::= '`' id '`' | id

imports ::= importStatement*
importStatement ::= 'import' fqn starImportSuffix? {pin=1 recoverWhile="declRecover"}
starImportSuffix ::= '.' '*'

namespaceDecl ::= 'namespace' fqn namespaceBody? {
  methods=[getFqn2 toString]
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaNamespaceDeclStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaNamespaceDeclStubElementType"
  pin=1
  recoverWhile="declRecover"
}

private namespaceBody ::= '{' namespaceBodyPart* '}'
private namespaceBodyPart ::= customParam // | ...

fqnSegment ::= qid {
  implements="com.intellij.psi.PsiNameIdentifierOwner"
  methods=[getName setName getNameIdentifier getSchemaFqn getSchemaFqnTypeRef isLast getReference getFqn]
}
fqn ::= fqnSegment ('.' fqnSegment)* { methods=[getFqn] }

private typeName ::= qid
typeRef ::= fqnTypeRef | anonList | anonMap {name="type"}
fqnTypeRef ::= fqn { methods=[resolve] }

defs ::= def*
private def ::= typeDefWrapper | supplementDef {recoverWhile="declRecover"}

typeDefWrapper ::= varTypeDef | recordTypeDef | mapTypeDef | listTypeDef | primitiveTypeDef | enumTypeDef {
  name="type definition"
  methods=[getElement delete toString]
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaTypeDefWrapperStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaTypeDefWrapperStubElementType"
}

anonList ::= 'list' '[' typeRef defaultOverride? ']' {pin=1}// TODO rename to `listType`? `concreteList`?
anonMap ::= 'map' '[' typeRef ',' typeRef defaultOverride? ']' {pin=1} // TODO rename to `mapType`?

extendsDecl ::= 'extends' typeRef (',' typeRef)* {pin=1}
supplementsDecl ::= 'supplements' fqnTypeRef (',' fqnTypeRef)* {pin=1}

defaultOverride ::= 'nodefault' | 'default' qid
customParam ::= qid '=' dataValue { // TODO embed data language instead
  name="custom attribute"
  implements="com.intellij.psi.PsiNamedElement"
  methods = [getName setName getNameIdentifier]
  pin = 2
}

metaDecl ::= 'meta' fqnTypeRef { pin=1 } // we don't allow anonList or anonMap as meta

varTypeDef ::= 'vartype' typeName extendsDecl? supplementsDecl? defaultOverride? varTypeBody? {
  methods=[supplemented]
  extends="com.sumologic.epigraph.schema.parser.psi.impl.SchemaVarTypeDefImplBase"
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaVarTypeDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaVarTypeDefStubElementType"
  pin=1
}
varTypeBody ::= '{' varTypeBodyPart* '}'
private varTypeBodyPart ::= varTypeMemberDecl | customParam
varTypeMemberDecl ::= 'override'? qid ':' typeRef memberBody? {
  implements=["com.sumologic.epigraph.schema.parser.psi.CustomParamsHolder" "com.intellij.psi.PsiNamedElement"]
  extends="com.sumologic.epigraph.schema.parser.psi.impl.CustomParamHolderImpl"
  methods=[getName setName getNameIdentifier getPresentation]
  pin=3
}
private memberBody ::= '{' memberBodyPart* '}'
private memberBodyPart ::= customParam

recordTypeDef ::= 'abstract'? 'polymorphic'? 'record' typeName extendsDecl? metaDecl? supplementsDecl? recordTypeBody? {
  methods=[supplemented]
  extends="com.sumologic.epigraph.schema.parser.psi.impl.SchemaRecordTypeDefImplBase"
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaRecordTypeDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaRecordTypeDefStubElementType"
  pin=3
}
recordTypeBody ::= '{' recordBodyPart* '}'
private recordBodyPart ::= fieldDecl | customParam
fieldDecl ::= 'override'? 'abstract'? qid ':' typeRef defaultOverride? fieldBody? {
  implements=["com.sumologic.epigraph.schema.parser.psi.CustomParamsHolder" "com.intellij.psi.PsiNamedElement"]
  extends="com.sumologic.epigraph.schema.parser.psi.impl.CustomParamHolderImpl"
  methods=[getName setName getNameIdentifier getPresentation]
  pin=4
}
private fieldBody ::= '{' fieldBodyPart* '}'
private fieldBodyPart ::= customParam

//listTypeDef2 ::= 'list' typeName extendsDecl listTypeBody? // allows inheritance from lists w/o type params
listTypeDef ::= 'abstract'? 'polymorphic'? anonList typeName extendsDecl? metaDecl? listTypeBody? {
  extends="com.sumologic.epigraph.schema.parser.psi.impl.SchemaListTypeDefImplBase"
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaListTypeDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaListTypeDefStubElementType"
  pin=3
}
listTypeBody ::= '{' listTypeBodyPart* '}'
private listTypeBodyPart ::= customParam

mapTypeDef ::= 'abstract'? 'polymorphic'? anonMap typeName extendsDecl? metaDecl? mapTypeBody? {
  extends="com.sumologic.epigraph.schema.parser.psi.impl.SchemaMapTypeDefImplBase"
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaMapTypeDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaMapTypeDefStubElementType"
  pin=3
}
mapTypeBody ::= '{' mapTypeBodyPart* '}'
private mapTypeBodyPart ::= customParam

enumTypeDef ::= 'enum' typeName metaDecl? enumTypeBody { // extends?
  extends="com.sumologic.epigraph.schema.parser.psi.impl.SchemaEnumTypeDefImplBase"
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaEnumTypeDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaEnumTypeDefStubElementType"
  pin=1
}
enumTypeBody ::= '{' enumTypeBodyPart* '}'
private enumTypeBodyPart ::= customParam | enumMemberDecl
enumMemberDecl ::= qid enumMemberBody? {
  implements=["com.sumologic.epigraph.schema.parser.psi.CustomParamsHolder" "com.intellij.psi.PsiNamedElement"]
  extends="com.sumologic.epigraph.schema.parser.psi.impl.CustomParamHolderImpl"
  methods=[getName setName getNameIdentifier]
  pin=1
}
private enumMemberBody ::='{' enumMemberBodyPart* '}'
private enumMemberBodyPart ::= customParam

primitiveTypeDef ::= 'abstract'? 'polymorphic'? primitiveKind typeName extendsDecl? metaDecl? primitiveTypeBody? {
  extends="com.sumologic.epigraph.schema.parser.psi.impl.SchemaPrimitiveTypeDefImplBase"
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaPrimitiveTypeDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaPrimitiveTypeDefStubElementType"
  pin=3
}
private primitiveKind ::= 'string' | 'integer' | 'long' | 'double' | 'boolean'
primitiveTypeBody ::= '{' primitiveBodyPart* '}'
private primitiveBodyPart ::= customParam // | ...

supplementDef ::= 'supplement' fqnTypeRef (',' fqnTypeRef)* 'with' fqnTypeRef {
  methods=[sourceRef supplementedRefs source supplemented getPresentation toString]
  stubClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaSupplementDefStub"
  elementTypeClass="com.sumologic.epigraph.schema.parser.psi.stubs.SchemaSupplementDefStubElementType"
  pin=1
}

// todo? for multi:  // not too good, no name for artificial vartype hence no common interface
// supplement User,Company with {
//    mySuperAnnotation = "sdf"; ??
//    foo : MyType { sdf = "sdf" }
// }

/*
supplement FooRec, BarRec with record { // not good since supplemented records won't have common interface for new field(s)
    newField: SomeType
}
*/

//combinedFqns ::= fqnTypeRef ('+' fqnTypeRef)*

private declRecover ::= ! ('import' | 'namespace' | 'polymorphic' | 'abstract' | 'record' |
                           'map' | 'list' | 'vartype' | 'enum' | 'supplement'|
                           'string' | 'integer' | 'long' | 'double' | 'boolean')
private partRecover ::= ! ('}' | qid | 'abstract' | 'override' )

// ------------- data language -------------

//dataValue ::= dataTypeSpec? (dataVar | dataVarValue)
//private dataVarValue ::= dataRecord | dataMap | dataList | dataEnum | dataPrimitiveValue | 'null'
//private dataTypeSpec ::= ( fqnTypeRef '/' )*
dataValue ::= dataTypeSpec? (dataVar | varValue)
private dataVarValue ::= dataTypeSpec? varValue   // because we want (foo/bar:baz) ?
private varValue ::= dataRecord | dataMap | dataList | dataEnum | dataPrimitiveValue | 'null'
private dataTypeSpec ::= ( fqnTypeRef '/' )*

dataVar ::= '<' dataVarEntry* '>' {pin=1 extends=dataValue}
dataVarEntry ::= qid ':' dataVarValue ','? {pin=2 recoverWhile=dataValueRecover}

dataRecord ::= '{' dataRecordEntry* '}' {pin=1 extends=dataValue}
dataRecordEntry ::= qid ':' dataValue ','? {pin=2 recoverWhile=dataValueRecover}

dataMap ::= '(' dataMapEntry* ')' {pin=1 extends=dataValue}
dataMapEntry ::= dataVarValue ':' dataValue ','? {pin=2 recoverWhile=dataValueRecover}

dataList ::= '[' (dataValue ','?)* ']' {pin=1 extends=dataValue}

dataEnum ::= qid {extends=dataValue}

dataPrimitiveValue ::= string | number {extends=dataValue}

private dataValueRecover ::= ! ( qid | dataPrimitiveValue | '}' | ')' | '>' | ']' | 'abstract' | 'override')
