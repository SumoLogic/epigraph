namespace ws.epigraph.tests

import ws.epigraph.tests.Person
import ws.epigraph.tests.PersonId
import ws.epigraph.tests.PersonRecord
import ws.epigraph.tests.User

transformer person: Person {
  inputProjection :id
  outputProjection :`record`(id, firstName, lastName)
}

transformer bestFriend: Person {
  inputProjection :id,
  outputProjection :`record`(bestFriend:id)
}

resource users: PersonMap {

  read {
    outputProjection { ;start: Long, ;count: Long, meta: (start, count) } [](
      :(
        id,
        `record`(
          id,
          firstName,
          lastName,
          bestFriend:(
            id,
            `record`( id, firstName, lastName, bestFriend:id, worstEnemy )
          )~~User:(
            `record`(
              worstEnemy( id, firstName, lastName, profile )
              profile
            )
          )
          worstEnemy( id, firstName, lastName ) ~UserRecord( profile )
          friends *(
            :(
              id,
              `record`( id, firstName, lastName )
            )
          )
        )
      )~~User:(
        id,
        `record`( profile, worstEnemy( profile ) )
      )
    )
  }

  read bestFriend {
    path /.:`record`/bestFriend
    outputProjection :`record`(id, firstName, lastName)
  }

  create {
    inputType list[PersonRecord]
    inputProjection *(
      +firstName,
      lastName,
      bestFriend:(
        +id,
        `record`( id, firstName, lastName, bestFriend:id, worstEnemy )
      )~~User:(
        `record`(
          worstEnemy( +id, firstName, lastName, profile )
          profile
        )
      )
      worstEnemy { default: { firstName: "Harry", lastName: "Potter" } } ( id, firstName, lastName )
      friends *(
        :(
          id,
          `record`( id, firstName, lastName )
        )
      )
    )

    outputType list[PersonId] // todo list[PersonId throws epigraph.Error]
  }

  create friends {
    path /.:`record`/friends
    inputProjection *:id
    outputProjection *:id  // id or error for failure; todo: use epigrph.Error instead
  }

  update {
    inputProjection [ required ](
      :(
        `record`(
          +firstName,
          lastName,
        )
      )
    )

    outputType map[PersonId, epigraph.Error]
    outputProjection [] (code, message)
  }

  update bestFriend {
    path /.:`record`/bestFriend

    inputProjection :+id

    outputType epigraph.Error
    outputProjection (code, message)
  }

  delete {
    deleteProjection [ required ] +

    outputType map[PersonId, epigraph.Error]
    outputProjection [] (code, message)
  }

  delete bestFriend {
    path /.:`record`/bestFriend

    deleteProjection +

    outputType epigraph.Error
    outputProjection (code, message)
  }

  // output projection generator test
  delete recursiveTest {
    deleteProjection [ ] ( $recTest = :`record` ( bestFriend $recTest ) )

    outputType map[PersonId, epigraph.Error]
    outputProjection [] (code, message)
  }

  custom capitalize {
    method POST
    path /.              // only applicable to a single map key

    inputType PersonRecord
    inputProjection (firstName, lastName)

    outputType PersonRecord
    outputProjection (firstName, lastName)
  }

  custom echo {
    method GET

    inputType Integer
    inputProjection { ;+param : PersonMap []:(id,`record`(id,firstName,bestFriend :id,friends *: id)) }

    outputType PersonMap
    outputProjection [ forbidden ]:(
      id,
      `record` (
        id,
        firstName,
        bestFriend :id,
        friends *:id
      )
    )
  }

}

resource user: Person {

  read {
    outputProjection :(
      id,
      `record`(
        id,
        firstName,
        lastName,
        bestFriend:(
          id,
          `record`( id, firstName, lastName, bestFriend:id, worstEnemy )
        )~~ws.epigraph.tests.User:(
          `record`(
            worstEnemy( id, firstName, lastName, profile )
            profile
          )
        )
        worstEnemy( id, firstName, lastName )
        friends *(
          :(
            id,
            `record`( id, firstName, lastName )
          ) ~~User:`record`(profile)
        )
      )
    )~~User:(
      id,
      `record`( profile, worstEnemy( profile ) )
    )
  }

}
