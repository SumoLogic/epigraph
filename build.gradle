plugins {
  id 'nebula.dependency-recommender' version '3.6.3'
  id 'nebula.maven-publish' version '4.9.1'
}

allprojects {
  apply plugin: 'idea'
  apply plugin: 'nebula.dependency-recommender'
  // https://github.com/nebula-plugins/nebula-dependency-recommender-plugin/issues/9
  apply plugin: 'nebula.maven-publish'

  group = theGroup
  version = theVersion

  dependencyRecommendations {
    mavenBom file: file("$rootDir/pom.xml")
  }
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    options.encoding = 'UTF-8'
  }
}

// importing gradle project from IDEA is preferred to using 'gradle idea'
idea {
  project {
    jdkName = javaVersion
    languageLevel = javaVersion
    vcs = 'Git'
  }
}

// bootstrap: gradle -c settings-bootstrap.gradle publishGradlePlugins

task publishGradlePlugins(dependsOn: [
    ':epigraph-scala-core:publishToMavenLocal',
    ':epigraph-java-codegen:publishToMavenLocal',
    ':epigraph-lang-common:publishToMavenLocal',
    ':epigraph-light-psi:publishToMavenLocal',
    ':epigraph-schema-parser:publishToMavenLocal',
    ':epigraph-gradle-plugins-common:publishToMavenLocal',
    ':epigraph-java-gradle-plugin:publishToMavenLocal',
    ':epigraph-schema-compiler:publishToMavenLocal',
    ':epigraph-schema-compiler-gradle-plugin:publishToMavenLocal'
]) {
  description 'Bootstraps gradle build by installing necessary plugins. Usage: \n gradle -c settings-bootstrap.gradle publishGradlePlugins'
}
